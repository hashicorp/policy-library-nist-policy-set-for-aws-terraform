# This policy requires neptune cluster to be deployed in multiple availability zone (multi-AZ) mode

# Copyright IBM Corp. 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                  "neptune-cluster-should-be-deployed-in-multi-az",
	"message":                      "Attribute 'availability_zones' must be greater than or equal to 2 for 'aws_neptune_cluster' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/neptune-controls.html#neptune-9 for more details.",
	"resource_aws_neptune_cluster": "aws_neptune_cluster",
}

# Functions
get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		availability_zones = maps.get(res, "values.availability_zones", "")
		return length(availability_zones) >= 2
	})
}

# Variables

aws_neptune_clusters = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_neptune_cluster).resources
violations = get_violations(aws_neptune_clusters)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
