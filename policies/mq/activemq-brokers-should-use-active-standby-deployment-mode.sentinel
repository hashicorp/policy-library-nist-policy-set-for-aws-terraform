# MQ.5
# This control checks whether the deployment mode for an Amazon MQ ActiveMQ broker is set to active/standby.

# Copyright IBM Corp. 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":            "activemq-brokers-should-use-active_standby-deployment-mode",
	"message":                "Amazon MQ brokers should use ACTIVE_STANDBY_MULTI_AZ or CLUSTER_MULTI_AZ deployment mode for high availability. https://docs.aws.amazon.com/securityhub/latest/userguide/mq-controls.html#mq-5",
	"resource_aws_mq_broker": "aws_mq_broker",
	"valid_deployment_modes": ["ACTIVE_STANDBY_MULTI_AZ", "CLUSTER_MULTI_AZ"],
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_mq_broker).resources
violations = collection.reject(resources, func(res) {
	deployment_mode = maps.get(res, "values.deployment_mode", "SINGLE_INSTANCE")
	return deployment_mode in const.valid_deployment_modes
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
