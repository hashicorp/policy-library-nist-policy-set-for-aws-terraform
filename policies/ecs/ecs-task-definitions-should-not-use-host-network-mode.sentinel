# ECS.17
# This control checks whether the latest active revision of an Amazon ECS task definition uses host network mode.

# Copyright IBM Corp. 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":                      "ecs-task-definitions-should-not-use-host-network-mode",
	"message":                          "ECS task definitions should not use 'host' network mode for security reasons. Refer to AWS security best practices for more details. https://docs.aws.amazon.com/securityhub/latest/userguide/ecs-controls.html#ecs-17",
	"resource_aws_ecs_task_definition": "aws_ecs_task_definition",
	"network_mode":                     "network_mode",
	"prohibited_network_mode":          "host",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_ecs_task_definition).resources
violations = collection.filter(resources, func(res) {
	network_mode = maps.get(res, "values.network_mode", "")
	return network_mode == const.prohibited_network_mode
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
