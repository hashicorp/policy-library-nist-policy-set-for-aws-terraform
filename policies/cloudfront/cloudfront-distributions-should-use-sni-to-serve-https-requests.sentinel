# This control checks whether 'aws_cloudfront_distribution' are using a custom SSL/TLS certificate and are configured to use SNI to serve HTTPS requests.

# Copyright (c) HashiCorp, Inc.
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                          "cloudfront-distributions-should-use-sni-to-serve-https-requests",
	"resource_aws_cloudfront_distribution": "aws_cloudfront_distribution",
	"message": "'aws_cloudfront_distribution' are using a custom SSL/TLS certificate and are configured to use SNI to serve HTTPS requests. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/cloudfront-controls.html#cloudfront-8 for more details.",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		viewer_certificate = maps.get(res.values, "viewer_certificate", [])
		ssl_support_method = maps.get(viewer_certificate[0], "ssl_support_method", "")
		return ssl_support_method is not null and ssl_support_method is "sni-only"
	})
}

# Variables

aws_cloudfront_distribution = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_cloudfront_distribution).resources
violations = get_violations(aws_cloudfront_distribution)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
