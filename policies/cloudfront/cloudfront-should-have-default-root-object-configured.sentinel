# This control checks whether an 'aws_cloudfront_distribution' is configured to return a specific object
# that is the default root object.

# Copyright (c) HashiCorp, Inc.
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                          "cloudfront-should-have-default-root-object-configured",
	"resource_aws_cloudfront_distribution": "aws_cloudfront_distribution",
	"message": "'aws_cloudfront_distribution' is configured to return a default root object. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/cloudfront-controls.html#cloudfront-1 for more details.",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		default_root_object = maps.get(res, "values.default_root_object", null)
		return default_root_object is not null
	})
}

# Variables

aws_cloudfront_distribution = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_cloudfront_distribution).resources
violations = get_violations(aws_cloudfront_distribution)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
